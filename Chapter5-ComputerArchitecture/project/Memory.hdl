// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including the RAM and memory mapped I/O.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 results in accessing the RAM;
 * Access to address 16384 to 24575 results in accessing the Screen memory map;
 * Access to address 24576 results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // '0b011111111111111' 16383
    // '0b100000000000000' 16384
    // '0b101111111111111' 24575
    // '0b110000000000000' 24576
    DMux4Way(in=load,sel=address[13..14],a=lram1,b=lram2,c=lscr,d=lkbd);
    Or(a=lram1,b=lram2,out=lram);
    RAM16K(in=in,load=lram,address=address[0..13],out=oram);
    Screen(in=in,load=lscr,address=address[0..12],out=oscr);
    Keyboard(out=ok);
    Mux4Way16(a=oram,b=oram,c=oscr,d=ok,sel=address[13..14],out=out);
}
